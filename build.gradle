plugins {
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java-library'
    id 'com.diffplug.spotless' version '6.25.0'
}

spotless {
    java {
        // 모든 Java 소스 파일에 포맷팅 규칙 적용
        target("**/*.java")
        // google 자바 포맷 적용
        googleJavaFormat()
        // 불필요한 임포트 제거 - 현재 미구현된 부분 있어서 기능 제거 추후 미구현 부분은 import에 '//'처리 필요함.
        // removeUnusedImports()
        // 마지막줄 New Line 처리
        endWithNewline()
        // 공백 제거
        trimTrailingWhitespace()
        //임포트 순서 정리
        importOrder(
                "java",
                "jakarta",
                "lombok",
                "org.springframework",
                "",
                "org.junit",
                "com.guide"
        )
    }
}

springBoot {
    mainClass = 'com.inhabas.api.ApiApplication'
}

repositories {
    mavenCentral()
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.inhabas'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    dependencies {
        annotationProcessor(
                'org.projectlombok:lombok',
                'org.springframework.boot:spring-boot-configuration-processor',
                'jakarta.persistence:jakarta.persistence-api',
                'jakarta.annotation:jakarta.annotation-api',
                'com.querydsl:querydsl-apt:5.0.0:jakarta'
        )

        implementation (
                'org.springframework.boot:spring-boot-starter-web',
                'org.springframework.boot:spring-boot-starter-validation',
                'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0',
                'com.google.code.findbugs:jsr305:3.0.2',

                // cloud config
                'org.springframework.cloud:spring-cloud-starter-config:4.1.4',
                'org.springframework.boot:spring-boot-starter-actuator',
                'org.springframework.cloud:spring-cloud-starter-bootstrap:4.1.4',

                // mail
                'org.springframework.boot:spring-boot-starter-mail',
                'com.amazonaws:aws-java-sdk-ses:1.12.188',
                'org.springframework.boot:spring-boot-starter-thymeleaf',

                // monitoring
                'io.micrometer:micrometer-registry-prometheus'
        )
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        compileOnly 'org.projectlombok:lombok'


        runtimeOnly(
                'org.mariadb.jdbc:mariadb-java-client',
                'com.h2database:h2'
        )
    }

    test {
        useJUnitPlatform()
    }

}

project(':module-jpa') {
    bootJar {enabled = false}
    jar {enabled = true}

    dependencies {
        api (
                'org.springframework.boot:spring-boot-starter-data-jpa',
                'com.querydsl:querydsl-jpa:5.0.0:jakarta',
                'com.jcraft:jsch:0.1.55',
//                'org.mariadb.jdbc:mariadb-java-client',
                'com.mysql:mysql-connector-j',
                'com.h2database:h2'
        )
    }
}

project(':module-auth') {
    bootJar {enabled = false}
    jar {enabled = true}

    dependencies {
        api project(':module-jpa')
        // jwt
        api 'io.jsonwebtoken:jjwt-api:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5',
                // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
                //'org.bouncycastle:bcprov-jdk15on:1.60',
                'io.jsonwebtoken:jjwt-jackson:0.11.5'

        // security
        api 'org.springframework.boot:spring-boot-starter-security'
        api 'org.springframework.boot:spring-boot-starter-oauth2-client'
        api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation 'org.mockito:mockito-inline:5.2.0'
    }
}

project(':module-fileStorage') {
    bootJar {enabled = false}
    jar {enabled = true}

    dependencies {
        api project(':module-jpa')

        api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
        implementation 'com.amazonaws:aws-java-sdk-s3:1.12.188'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation 'org.mockito:mockito-inline:5.2.0'
    }
}


project(':resource-server') {
    dependencies {
        api project(':module-auth')
        api project(':module-fileStorage')
        api 'org.springframework.boot:spring-boot-starter-security'
        implementation 'com.amazonaws:aws-java-sdk-s3:1.12.188'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    clean {
        delete file('src/main/generated')
    }

    task cleanGeneratedDir(type: Delete) {
        delete file('src/main/generated')
    }
}